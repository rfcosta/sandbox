var logLocation = '/var/log/shp/gen_alerts.log'

var execScript = '/opt/aed/shp/bin/GenerateEvent.py'

var metric = stream
    |from()
        .database('kpi')
        .retentionPolicy('days')
        .measurement('metric')
        .groupBy('ci', 'key')
    |where(lambda: "type" == 'error_rate' AND "need_to_alert_dynamic" == 'True')
    |log()
        .prefix('dynamic_data')

var thresholds = stream
    |from()
        .database('kpi')
        .retentionPolicy('days')
        .measurement('thresholds')
        .groupBy('ci', 'key')
    |where(lambda: "type" == 'dynamic' AND "metric" == 'error_rate' AND "need_to_alert" == 'True')
    |log()
        .prefix('dynamic_trend')

metric
    |join(thresholds)
        .as('metric', 'thresholds')
        .tolerance(15s)
    |log()
        .prefix('dynamic_join')
    |alert()
        .id('kpi_Dynamic_Error_Rate_Upper_{{ index .Tags "ci" }}_{{ index .Tags "key" }}')
        .message('Error Rate')
        .details('{{ .Level }}: {{ index .Tags "ci" }} error rate is {{ index .Fields "metric.error_rate" }}')
        .warn(lambda: "thresholds.error_rate_crit_upper" != float(-1) AND float("metric.error_rate") > "thresholds.error_rate_crit_upper")
        .log(logLocation)
        .exec('python', execScript)

var logLocation = '/var/log/shp/gen_alerts.log'

var execScript = '/opt/aed/shp/bin/GenerateEvent.py'


var metric = stream
    |from()
        .database('kpi')
        .retentionPolicy('days')
        .measurement('metric')
        .groupBy('ci', 'key')
    |where(lambda: "type" == 'avg_processing_time' AND "need_to_alert_dynamic" == 'True')
    |log()
        .prefix('dynamic_data')

var thresholds = stream
    |from()
        .database('kpi')
        .retentionPolicy('days')
        .measurement('thresholds')
        .groupBy('ci', 'key')
    |where(lambda: "type" == 'dynamic' AND "metric" == 'avg_processing_time' AND "need_to_alert" == 'True')
    |log()
        .prefix('dynamic_trend')

metric
    |join(thresholds)
        .as('metric', 'thresholds')
        .tolerance(15s)
    |log()
        .prefix('dynamic_join')
    |alert()
        .id('kpi_Dynamic_Average_Processing_Time_Upper_{{ index .Tags "ci" }}_{{ index .Tags "key" }}')
        .message('Processing Time')
        .details('{{ .Level }}: {{ index .Tags "ci" }} average processing time is {{ index .Fields "metric.avg_processing_time" }}ms')
        .warn(lambda: "thresholds.avg_processing_time_crit_upper" != float(-1) AND float("metric.avg_processing_time") > "thresholds.avg_processing_time_crit_upper")
        .log(logLocation)
        .exec('python', execScript)

metric
    |join(thresholds)
        .as('metric', 'thresholds')
        .tolerance(15s)
    |alert()
        .id('kpi_Dynamic_Average_Processing_Time_Lower_{{ index .Tags "ci" }}_{{ index .Tags "key" }}')
        .message('Processing Time')
        .details('{{ .Level }}: {{ index .Tags "ci" }} average processing time is {{ index .Fields "metric.avg_processing_time" }}ms')
        .warn(lambda: "thresholds.avg_processing_time_crit_lower" != float(-1) AND float("metric.avg_processing_time") < "thresholds.avg_processing_time_crit_lower")
        .log(logLocation)
        .exec('python', execScript)